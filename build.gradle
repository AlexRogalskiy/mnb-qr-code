plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'

}

group 'com.github.pozo'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.guava:guava:28.0-jre'

    testCompile 'io.nayuki:qrcodegen:1.5.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'mnb-qr-code'

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'mnb-qr-code'
                description = 'Hungarian instant payment QR code generator/parser library'
                url = 'https://github.com/Pozo/mnb-qr-code'
                licenses {
                    license {
                        name = 'GNU General Public License (GPL)'
                        url = 'http://www.gnu.org/licenses/gpl-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'pozo'
                        name = 'Zoltan Polgar'
                        email = 'pozo@gmx.com'
                    }
                }
                scm {
                    url = 'https://github.com/Pozo/mnb-qr-code/tree/master'
                    connection = 'scm:git:git://github.com/Pozo/mnb-qr-code.git'
                    developerConnection = 'scm:git:ssh://github.com:Pozo/mnb-qr-code.git'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username "${nexusUsername}"
                password "${nexusPassword}"
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.maven
}